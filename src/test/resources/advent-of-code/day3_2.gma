let test-input = {
    "00100"
    "11110"
    "10110"
    "10111"
    "10101"
    "01111"
    "00111"
    "11100"
    "10000"
    "11001"
    "00010"
    "01010"
}


let determine-winning-bit select-most-bits? count-0 count-1 =
    select-most-bits?
    ? count-1 ≥ count-0 ? "1" : "0"
    : count-0 ≤ count-1 ? "0" : "1"




let bits-in-position position input =
    input
    |> map [line : at position line]

assert
    "bits-in-position"
    (bits-in-position 0 test-input → {"0", "1", "1", "1", "1", "0", "0", "1", "1", "1", "0", "0"})
    (bits-in-position 4 test-input → {"0", "0", "0", "1", "1", "1", "1", "0", "0", "1", "0", "0"})




let determine-bit position select-most-bits? input =

    let count-0 = bits-in-position position input |> filter [bit : bit = "0"] |> size
    let count-1 = bits-in-position position input |> filter [bit : bit = "1"] |> size

    determine-winning-bit select-most-bits? count-0 count-1

assert
    "determine-bit"
    (determine-bit 0 true test-input → "1")
    (determine-bit 0 false test-input → "0")
    (determine-bit 4 true test-input → "0")
    (determine-bit 4 false test-input → "1")




let find-common-bits position select-most-bits? input =

    let bit = determine-bit position select-most-bits? input

    let matching-lines = input |> filter [line : (at position line) = bit]

    when {
        matching-lines.size = 1 ∨ position = matching-lines.first.size
            -> matching-lines.first

        else -> find-common-bits (position + 1) select-most-bits? matching-lines
    }



let find-most-common-bits = find-common-bits 0 true

let find-least-common-bits = find-common-bits 0 false




let make-binary-from-list number-list =
    number-list
    |> to-char-list
    |> map [ number :
        number > 0 ? 1 : 0 ]


let make-number-from-binary list =
    list
    |> fold 0 [ acc i :
        acc * 2 + i
    ]





let produce-result input =

    let oxygen-rate =
        find-most-common-bits input
        |> make-binary-from-list
        |> make-number-from-binary

    let co2-rate =
        find-least-common-bits input
        |> make-binary-from-list
        |> make-number-from-binary

    oxygen-rate * co2-rate




test-input
|> produce-result
|> print



read-lines "src/test/resources/advent-of-code/day3.input"
|> produce-result
