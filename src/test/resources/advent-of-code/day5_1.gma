let test-input = {
    "0,9 -> 5,9"
    "8,0 -> 0,8"
    "9,4 -> 3,4"
    "2,2 -> 2,1"
    "7,0 -> 7,4"
    "6,4 -> 2,0"
    "0,9 -> 2,9"
    "3,4 -> 1,4"
    "0,0 -> 8,8"
    "5,5 -> 8,2"
}



let max a b =
    a > b ? a : b

assert
    "max"
    (max 1 2 → 2)
    (max "1" "2" → "2")
    (max "2" "11" → "11")

let min a b =
    a < b ? a : b

assert
    "min"
    (min 1 2 → 1)
    (min "1" "2" → "1")
    (min "2" "11" → "2")






let split-line-into-coordinates line =
    let components = split line
    let start-cmd = split-by "," components.first
    let end-cmd = split-by "," components.last

    (start-cmd.first + 0 → start-cmd.last + 0) → (end-cmd.first + 0 → end-cmd.last + 0)

assert
    "split-line-into-coordinates"
    (split-line-into-coordinates "0,9 → 5,9" → {{0,9},{5,9}})
    (split-line-into-coordinates "8,0 → 0,8" → {{8,0},{0,8}})




let is-horizontal? coordinates =
    is-not-empty? coordinates ∧
    coordinates.first.last = coordinates.last.last

let is-vertical? coordinates =
    is-not-empty? coordinates ∧
    coordinates.first.first = coordinates.last.first



let is-strait? coordinates =
    is-vertical? ∨ is-horizontal?

assert
    "is-strait?"
    (is-horizontal? {{"0","9"},{"5","9"}} → true)
    (is-vertical? {{"8","0"},{"0","8"}} → false)
    (is-horizontal? {} → false)
    (is-vertical? {} → false)




let find-max-coordinates list =
    let max-x! = 0
    let max-y! = 0
    list ▷ for-each [coord →
        set max-x! = is-not-empty? coord ? max (max coord.first.first coord.last.first) max-x! : max-x!
        set max-y! = is-not-empty? coord ? max (max coord.first.last coord.last.last) max-y! : max-y!
    ]
    (max-x! → max-y!)

assert
    "find-max-coordinates"
    (test-input ▷ map split-line-into-coordinates ▷ find-max-coordinates → {9,9})
    ({{{1,1},{1,5}}, {{4,1},{2,1}}} ▷ find-max-coordinates → {4,5})
    ({{{1,1},{1,5}}, {}} ▷ find-max-coordinates → {1,5})







let create-field x-max y-max =
    0 .. y-max ▷ map [line → 0 .. x-max ▷ map [i → 0]]

assert
    "create-field"
    (create-field 2 2 → {{0,0,0},{0,0,0},{0,0,0}})






let make-line coord =
    let make-horizontal-line coord =
        coord.first.first .. coord.last.first ▷ map [it → {it, coord.first.last}]

    let make-vertical-line coord =
        coord.first.last .. coord.last.last ▷ map [it → {coord.first.first, it}]

    when
        (coord.first.first = coord.last.first → make-vertical-line coord)
        (coord.first.last = coord.last.last → make-horizontal-line coord)
        (else → {})

assert
    "make-line"
    (make-line {{0,0},{2,0}} → {{0,0},{1,0},{2,0}})
    (make-line {{0,2},{0,0}} → {{0,2},{0,1},{0,0}})
    (make-line {{0,0},{2,2}} → {})





let fill-horizontal-line field line =
    let y-pos = line.first.last
    let x-pos-start = min line.first.first line.last.first
    let x-pos-end = max line.first.first line.last.first

    (y-pos > 0 ? field ▷ slice 0 (y-pos) : ())
    @
    { field ▷ at y-pos ▷ map* [col pos →
        pos < x-pos-start ∨ pos > x-pos-end
        ? col
        : col + 1 ] }
    @
    (y-pos < field.size - 1 ? slice (y-pos + 1) (field.size - y-pos - 1) field : ())


assert
    "fill-horizontal-line"
    (fill-horizontal-line (create-field 2 2) {{1,0},{2,0}} → {{0, 1, 1}, {0, 0, 0}, {0, 0, 0}})
    (fill-horizontal-line (create-field 2 2) {{0,1},{2,1}} → {{0, 0, 0}, {1, 1, 1}, {0, 0, 0}})
    (fill-horizontal-line (create-field 2 2) {{0,2},{2,2}} → {{0, 0, 0}, {0, 0, 0}, {1, 1, 1}})


let fill-vertical-line field line =
    let y-pos-start = min line.first.last line.last.last
    let y-pos-end = max line.first.last line.last.last
    let x-pos = line.first.first

    (y-pos-start > 0 ? field ▷ slice 0 (y-pos-start) : ())
    @
    (y-pos-start .. y-pos-end ▷ map [ line-index →
        field
        ▷ at line-index
        ▷ map* [col pos →
            pos ≠ x-pos
            ? col
            : col + 1 ] ] )
    @
    (y-pos-end < field.size - 1 ? slice (y-pos-end + 1) (field.size - y-pos-end - 1) field : ())


assert
    "fill-vertical-line"
    (fill-vertical-line (create-field 2 2) {{0,1},{0,2}} → {{0, 0, 0}, {1, 0, 0}, {1, 0, 0}})
    (fill-vertical-line (create-field 2 2) {{1,0},{1,2}} → {{0, 1, 0}, {0, 1, 0}, {0, 1, 0}})
    (fill-vertical-line (create-field 2 2) {{2,0},{2,2}} → {{0, 0, 1}, {0, 0, 1}, {0, 0, 1}})






let set-positions-from-line field line =
    when
        (is-horizontal? line → fill-horizontal-line field line)
        (is-vertical? line → fill-vertical-line field line)
        (else → field)

assert
    "set-positions-from-list"
    (set-positions-from-line (create-field 2 2) {{1,0},{2,0}} → {{0, 1, 1}, {0, 0, 0}, {0, 0, 0}})
    (set-positions-from-line (create-field 2 2) {{1,1},{1,2}} → {{0, 0, 0}, {0, 1, 0}, {0, 1, 0}})
    (set-positions-from-line (create-field 2 2) {} → {{0, 0, 0}, {0, 0, 0}, {0, 0, 0}})




let count-fields-over-1 field =
    let count! = 0
    field
    ▷ for-each [ line →
        line ▷ for-each [ field →
            field > 1
            ? set count! = count! + 1
            : () ] ]
    count!

assert
    "count-fields-over-1"
    ({{0,1,2},{1,0,2}} ▷ count-fields-over-1 → 2 )




let produce-result input =
    let lines =
        input
        ▷ map split-line-into-coordinates
        ▷ map make-line

    let max-coord = find-max-coordinates lines

    let field = create-field max-coord.first max-coord.last

    let final-field = lines ▷ fold field [acc line → set-positions-from-line acc line]

    final-field ▷ count-fields-over-1


test-input
▷ produce-result
▷ print


read-lines "src/test/resources/advent-of-code/day5.input"
▷ produce-result
